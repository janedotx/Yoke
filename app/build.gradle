apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.system2override.yoke"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 2
        versionName "1.01"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    lintOptions{
        checkReleaseBuilds false
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res/layouts/app_limits',
                            'src/main/res/layouts/banned_apps',
                            'src/main/res/layouts',
                            'src/main/res'
                    ]
        }

        // for testing migrations
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
//        test.resources.srcDirs += files("$projectDir/sample_json".toString())

        androidTest {
            java.srcDirs += "$projectDir/src/sharedTest"
            resources.srcDirs += files("$projectDir/src/sharedTest/resources".toString())
        }

        test {
            java.srcDirs += "$projectDir/src/sharedTest"
            resources.srcDirs += files("$projectDir/src/sharedTest/resources".toString())
        }

    }
    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "DATABASE_FILE", "\"db\""
        }

        debug {
            applicationIdSuffix ".debug"
            buildConfigField "String", "DATABASE_FILE", "\"test-db\""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v13:27.1.1'
    implementation 'com.android.support:support-media-compat:27.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'com.android.support:design:27.1.1'


    implementation 'com.android.support:support-v4:27.1.1'
    testImplementation 'junit:junit:4.12'
//    testImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation('com.android.support.test.espresso:espresso-core:3.0.2') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305:2.0.1'
    }

    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation "org.robolectric:robolectric:3.8"
    testImplementation 'org.mockito:mockito-core:2.+'
    testImplementation 'org.powermock:powermock-module-junit4:2.+',
            'org.powermock:powermock-api-mockito2:2.+'



    implementation 'com.google.android.gms:play-services-identity:15.0.0'
    implementation 'com.google.android.gms:play-services-auth:15.0.0'

    // for gtasks integration
    implementation 'pub.devrel:easypermissions:0.3.0'

    implementation 'com.google.code.gson:gson:2.8.5'


    implementation('com.google.api-client:google-api-client-android:1.23.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-tasks:v1-rev52-1.23.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    // Room (use 1.1.0-beta1 for latest beta)
    implementation "android.arch.persistence.room:runtime:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"
    // Test helpers for Room
    testImplementation "android.arch.persistence.room:testing:1.0.0"

    implementation 'com.squareup:otto:1.3.8'

    implementation "commons-io:commons-io:+"

    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
}
